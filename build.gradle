import com.github.sherter.googlejavaformatgradleplugin.GoogleJavaFormat
import com.github.sherter.googlejavaformatgradleplugin.VerifyGoogleJavaFormat

buildscript {
    repositories {
        google()
        mavenCentral()
        //noinspection JcenterRepositoryObsolete
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id 'com.github.sherter.google-java-format' version '0.6'
    id 'com.github.hierynomus.license' version '0.14.0'
}

allprojects {
    repositories {
        google()
        mavenCentral()
        //noinspection JcenterRepositoryObsolete
        jcenter()
    }
}

task format(type: GoogleJavaFormat) {
    exclude '**/package-info.java'
}
task verifyFormat(type: VerifyGoogleJavaFormat) {
    exclude '**/package-info.java'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// Define versions in a single place
ext {
    // Sdk and tools
    minSdkVersion = 15
    targetSdkVersion = 28
    compileSdkVersion = 28
    buildToolsVersion = '28.0.3'

    // App dependencies
    supportLibraryVersion = '28.0.0'
    guavaVersion = '28.0-android'
    junitVersion = '4.13.2'
    mockitoVersion = '3.11.2'
    powerMockito = '1.6.2'
    hamcrestVersion = '1.3'
    runnerVersion = '1.0.0'
    rulesVersion = '1.0.0'
    espressoVersion = '3.0.1'
    rxjavaVersion = '2.1.9'
    rxandroidVersion = '2.0.1'
    sqlbriteVersion = '2.0.0'
    mobius = '1.2.0'
}

allprojects {
    apply plugin: 'com.github.hierynomus.license'

    license {
        header rootProject.file('apache2.header')
        exclude("**/AutoValue_*.java")
        mapping("java", "SLASHSTAR_STYLE")
    }

    getTasksByName('check', false).each { checkTask -> checkTask.dependsOn(tasks.withType(Javadoc)) }
}
